grammar org.xtext.sdu.IoT with org.eclipse.xtext.common.Terminals

generate ioT "http://www.xtext.org/sdu/IoT"

System:
	system+=(SensorTypes | Sensor | SensorGroup | SensorGetMethod | DeviceTypes | Device | ServerTypes | Server | DestinationTypes | Destination | FetchData)+
;

SensorType:
	name=ID
;

SensorTypes:
	'SensorTypes' types+=SensorType (',' types+=SensorType)*
;

Sensor:
	'Sensor' name=ID 'of' 'type' type=[SensorType]
;

SensorGroup:
	'SensorGroup' name=ID 'include' sensors+=[Sensor] (',' sensors+=[Sensor])*
;

SensorGetMethod:
	'SensorGetMethod' method=Method  'for' 'type' type=[SensorType]
;

Method: 
	name=ID '(' ((parameters+=ID) (',' parameters+=ID)*)? ')'
;

DeviceType:
	name=ID
;

DeviceTypes:
	'DeviceTypes' types+=DeviceType (',' types+=DeviceType)*
;

Device:
	'Device' name=ID 'of' 'type' type=[DeviceType] 'contains' ('device' devices=[Device] | 'group' devices=[SensorGroup])
;

ServerType:
	name=ID
;

ServerTypes:
	'ServerTypes' types+=ServerType (',' types+=ServerType)*
;

Server:
	'Server' name=ID 'of' 'type' type=[ServerType] 'listens' 'on'  ip=Ip 'and' port=Portnumber
;

DestinationType:
	name=ID
;

DestinationTypes:
	'DestinationTypes' types+=DestinationType (',' types+=DestinationType)*
;

Destination:
	'Destination' name=ID 'of' type=[DestinationType]
;

FetchData:
	'Fetch' 'data' 'from' ('type' filter=[SensorType] | 'group' filter=[SensorGroup]) 'for' 'device' device=[Device] 'to' ('destination' destination=[Destination] | 'server' destination=[Server]) (=>'condition' condition=FetchDataCondition)? (=>'triggered' triggered=FetchDataExpression)?
;

FetchDataExpression:
	'every' duration=Time timeUnit=TimeUnit
;

FetchDataCondition:
	'if'('(' condition=Condition ')') (=>'else' else=FetchDataCondition)?
;

TimeUnit:
	('ms' | 's' | 'm' | 'h')
;

Condition:
	OrCondition
;

OrCondition returns Condition:
    AndCondition ({OrCondition.left=current} "||" right=AndCondition)*  
;   

AndCondition returns Condition:
    ComparisonCondition ({AndCondition.left=current} "&&" right=ComparisonCondition)*  
;

ComparisonCondition returns Condition:
    PrimaryCondition ({ComparisonCondition.left=current} operator=("<"|"<="|"=="|">="|">") right=PrimaryCondition)*
;

PrimaryCondition returns Condition:
    "(" Condition ")" | LiteralBoolean | LiteralInteger | LiteralMethod
;

LiteralBoolean returns Condition:
     {LiteralBool} value="true" | {LiteralBool} value="false"
;

LiteralMethod returns Condition:
	Method
;

LiteralInteger returns Condition:
	{LiteralNumber} value=INT
;

Ip:
	{Ip} ip+=INT '.' ip+=INT '.' ip+=INT '.' ip+=INT
;

Portnumber:
	{Portnumber} number=INT
;

Time:
	time=INT
;